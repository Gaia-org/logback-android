plugins {
    id 'maven-publish'
}
apply plugin: 'com.android.library'

android {
    namespace 'com.github.tony19.logback.android'
    compileSdkVersion 31
    buildToolsVersion '30.0.3'

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_6
        targetCompatibility JavaVersion.VERSION_1_6
    }

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 31
        versionCode VERSION_CODE.toInteger()
        versionName VERSION_NAME
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        checkAllWarnings true
        lintConfig rootProject.file('gradle/lint.xml')
    }
    testOptions {
        unitTests {
            includeAndroidResources = true

            all {
                testLogging {
                    events 'failed'
                    showStackTraces = true
                    exceptionFormat = 'full'
                }
            }
        }
    }
    publishing {
        singleVariant('release') {
            withSourcesJar()
            withJavadocJar()
        }
    }
}
afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId GROUP
                artifactId 'logback-android'
                version = VERSION_NAME

                pom {
                    name = 'logback-android'
                    description = 'Logging library for Android apps'
                    url = 'https://github.com/tony19/logback-android'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'tony19'
                            name = 'Tony Trinh'
                            email = 'tony19@gmail.com'
                        }
                    }
                    scm {
                        connection = 'scm:git@github.com:tony19/logback-android.git'
                        developerConnection = 'scm:git@github.com:tony19/logback-android.git'
                        url = 'https://github.com/tony19/logback-android'
                    }
                }
            }
        }
    }
}
dependencies {

    testImplementation('junit:junit:4.13.2') {
        exclude(module: 'hamcrest-core')
    }

    testImplementation 'org.hamcrest:hamcrest-junit:2.0.0.0'
    testImplementation 'org.robolectric:robolectric:4.9'
    testImplementation 'org.mockito:mockito-core:2.28.2'
    testImplementation 'joda-time:joda-time:2.12.1'

    // This dep is required when using Robolectric and targeting pre-21 SDKs.
    // Otherwise, tests fail with java.lang.NoClassDefFoundError.
    // https://github.com/robolectric/robolectric/issues/2562#issuecomment-263086503
    testImplementation 'org.robolectric:android-all:13-robolectric-9030017'
    testImplementation 'com.icegreen:greenmail:1.6.11'
    testImplementation 'dom4j:dom4j:1.6.1'
    testImplementation 'org.easytesting:fest-assert:1.4'
    testImplementation "org.slf4j:integration:${slf4jVersion}"
    testImplementation "org.slf4j:log4j-over-slf4j:${slf4jVersion}"
    testImplementation "org.slf4j:slf4j-api:${slf4jVersion}:tests"
    testImplementation "org.slf4j:slf4j-ext:${slf4jVersion}"
    testImplementation(group: 'org.subethamail', name: 'subethasmtp', version: '3.1.7') {
        exclude(module: 'slf4j-api')
    }
    testImplementation project(path: ':logback-android')

    compileOnly "org.slf4j:slf4j-api:${slf4jVersion}"

    // For SMTPAppender
    compileOnly 'com.sun.mail:android-mail:1.6.7'
    compileOnly 'com.sun.mail:android-activation:1.6.7'
}

allprojects {
    tasks.withType(JavaCompile) {
        // Warn about deprecations
        //options.compilerArgs << '-Xlint:deprecation'
        // Warn about unchecked usages
        options.compilerArgs << '-Xlint:unchecked'
        // Don't warn about using source/target 1.5 option
        options.compilerArgs << '-Xlint:-options'

        options.debug(['debugLevel': 'source,lines,vars'])
        options.debug = VERSION_NAME.contains('SNAPSHOT')
    }

    tasks.withType(AbstractArchiveTask) {
        preserveFileTimestamps = false
        reproducibleFileOrder = true
    }
}
